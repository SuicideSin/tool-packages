# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=devkitARM
pkgver=r50
pkgrel=3
pkgdesc='Toolchain for ARM based development'
arch=('any')
url='https://devkitpro.org'
license=('GPL')
options=(!strip libtool staticlibs)

if [ "$CHOST" = "x86_64-w64-mingw32" ]; then
  source=("https://github.com/devkitPro/buildscripts/releases/download/${pkgname}_${pkgver}/${pkgname}_${pkgver}-win64.tar.xz")
  sha256sums=('b9d04adf577aaaa26e10a12f0f0364dd9579e9d3ef1d15265553134214a8f837')
fi

if [ "$CHOST" = "x86_64-apple-darwin15" ]; then
  source=("https://github.com/devkitPro/buildscripts/releases/download/${pkgname}_${pkgver}/${pkgname}_${pkgver}-osx.tar.xz")
  sha256sums=('4b7cbb6eee3a160fd42fc2967861b115db780ee838b0a6817d4c0cffe85960d7')
fi

if [ "$CHOST" = "x86_64-pc-linux-gnu" ]; then
  source=("https://github.com/devkitPro/buildscripts/releases/download/${pkgname}_${pkgver}/${pkgname}_${pkgver}-linux.tar.xz")
  sha256sums=('43c912f5007d889a8585973f02de226daa4efcfc91fa472e1d315095a56327d9')
fi

source+=("dirent.h.patch" "iosupport.h.patch")
sha256sums+=(
  '223cfc90a91f234171a3721d4b91ce33db8421b29e5953a2a88e7466afc0f843'
  '2257e16b101d985c1cddcedf79463313b245ce5b836627f09b6e0ade3555882d'
)

groups=('gp32-dev' 'gba-dev' 'nds-dev' '3ds-dev')

package() {

  mkdir -p "$pkgdir"/opt/devkitpro
  cp -R "$pkgname" "$pkgdir"/opt/devkitpro
  patch -i "$srcdir"/dirent.h.patch "$pkgdir"/opt/devkitpro/devkitARM/arm-none-eabi/include/sys/dirent.h  
  patch -i "$srcdir"/iosupport.h.patch "$pkgdir"/opt/devkitpro/devkitARM/arm-none-eabi/include/sys/iosupport.h  
  
}
